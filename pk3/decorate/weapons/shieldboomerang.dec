actor ShieldBoomerangWep_NormalBar : MMZNormalBar {}

actor ShieldBoomerangWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/Ranged
//$Title Dust Crusher
//$Sprite WEA2K0
Weapon.AmmoUse 1
Weapon.AmmoGive 100
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_SHIELDBOOMERANG"
Obituary "$OB_SHIELDBOOMERANG"
Tag "$TAG_SHIELDBOOMERANG"
weapon.ammotype "ShieldBoomerangAmmo"
inventory.icon "SHLDBOOM"
States
{
SpawnLoop:
RRSB Z -1
loop
Ready:
RRSB L 0 ACS_NamedExecuteWithResult("core_weaponcolor",RR_CLR_RECOILROD)
RRSB AB 2 A_WeaponReady(RR_RECOILROD_READY)
RRSB CDCD 2 A_WeaponReady(RR_RECOILROD_READY)
RRSB HFGFGFGFGFGHI 1 A_WeaponReady(RR_RECOILROD_READY)
ReadyLoop:
RRSB CD 2 A_WeaponReady(RR_RECOILROD_READY)
loop
ReadyRaise:
RRSB AB 2
goto ReadyLoop
Deselect:
DSTS B 0
goto DeselectSwap
Select:
DSTS B 0
goto SelectSwap
Fire:
RRSB J 1
RRSB J 0 ACS_NamedExecuteWithResult("rr_justframes", BT_ATTACK|BT_ALTATTACK, 5)
RRSB JKK 1 A_JumpIfInventory("ReptileJustFrames", 1, "Throw")
Hold:
RRSB L 0 ACS_NamedExecuteWithResult("rr_justframes", BT_ATTACK|BT_ALTATTACK, 5)
RRSB L 0 A_GiveInventory("ShieldBoomerangProtect", 1)
RRSB LMNO 1 A_JumpIfNoAmmo("ShieldBreak")
RRSB L 0 A_Refire
RRSB L 0 A_TakeInventory("ShieldBoomerangProtect", 1)
RRSB L 0 A_JumpIfInventory("ReptileJustFrames", 1, "Throw")
RRSB L 0 ACS_NamedExecuteWithResult("rr_justframes", BT_ATTACK|BT_ALTATTACK, 5)
RRSB KKJJ 1 A_JumpIfInventory("ReptileJustFrames", 1, "Throw")
goto ReadyLoop
ShieldBreak:
RRSB K 0 A_PlaySoundEx("reptile/weapons/shieldboomerangbreak", "Weapon")
RRSB O 1 offset(12, 50)
RRSB O 1 offset(24, 68)
RRSB O 1 offset(18, 59)
RRSB O 1 offset( 9, 46)
RRSB O 0 A_WeaponReady(WRF_RESETOFFSET)
goto ShieldWait
Throw:
RRS2 ABCD 1
RRS2 E 5
RRS2 E 0 A_PlaySoundEx("reptile/weapons/shieldboomerangthrow2", "Weapon")
RRS2 E 0 A_FireCustomMissile("ShieldBoomerang", 0, 0, 0, 0)
RRS2 FGHI 1 A_JumpIfInventory("ShieldBoomerangReturn", 1, "ReadyRaise")
RRS2 JJJJJJJJJJ 1 A_JumpIfInventory("ShieldBoomerangReturn", 1, "ReadyRaise")
RRS2 KLMN 1 A_JumpIfInventory("ShieldBoomerangReturn", 1, "ReadyRaise")
ShieldWait:
RRS2 O 1 A_JumpIfInventory("ShieldBoomerangReturn", 1, "ReadyRaise")
wait
NoAmmo:
DSTS B 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor ShieldBoomerangAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 100
+INVENTORY.IGNORESKILL
}

actor ShieldBoomerangReturn : ReptileFlag2 {}
actor ShieldBoomerangProtect : ReptileFlag5 {}

actor ShieldBoomerangBase : BasicProjectile
{
Radius 16
Height 16
damage (35)
damagetype "ShieldBoomerang"
Obituary "$OB_SHIELDBOOMERANG"
+DONTSPLASH
+RIPPER
speed 40
Args 40 //return speed
var int user_TranslateWhiteColors;
var int user_pierceRipper;
scale 2.0
}

actor ShieldBoomerang : ShieldBoomerangBase
{
+DOOMBOUNCE
+SKYEXPLODE
bouncefactor 1.0
wallbouncefactor 1.0
+HITTRACER
bouncecount 2
reactiontime 30 // max time
States
{
Spawn:
RRSB Q 0
RRSB Q 0 A_PlaySoundEx("reptile/weapons/shieldboomerangfly", "Body", true)
SpawnLoop:
RRSB QRST 1 A_GiveInventory("ShieldBoomerangFlyFunc")
loop
Death:
RRSB Q 0 A_StopSoundEx("Body")
RRSB Q 0 A_JumpIf(reactiontime == 350, "Disappear")
DeathEnd:
TNT1 A 0 A_GiveInventory("BouncerReflectedCheck",1)
TNT1 A 0 A_SpawnItemEx("ReturningShieldBoomerangMaster")
TNT1 A 4
stop
Disappear:
TNT1 A 0 A_PlaySoundEx("reptile/weapons/shieldboomerangdisappear", "Body")
goto DeathEnd
}
}

actor ShieldBoomerangFlyFunc : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_GiveInventory("BouncerReflectedCheck",1)
        TNT1 A 0 A_Countdown
        stop
    }
}

actor ReturningShieldBoomerangMaster : ShieldBoomerangBase
{
+NOCLIP
+NOINTERACTION
reactiontime 350
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("reptile/weapons/shieldboomerangfly", "Body", true)
TNT1 A 0 A_SpawnItemEx("ReturningShieldBoomerang",0,0,0,0,0,0,0,0,0,CallACS("core_NewUniqueTID"))
TNT1 A 0 ACS_NamedExecuteWithResult("core_setTIDsPointerToThis",CallACS("core_CurrentUniqueTID"),AAPTR_MASTER)
TNT1 A 0 ACS_NamedExecuteWithResult("core_ClearUniqueTID",0)
RRSB QRSTQRST 1 A_GiveInventory("ReturningShieldBoomerangMasterFunc",1)
SpawnLoop:
RRSB QRSTQRST 1 A_GiveInventory("ReturningShieldBoomerangMasterFunc",1)
loop
Death:
TNT1 A 0 A_StopSoundEx("Body")
TNT1 A 0 A_RemoveChildren(true)
stop
}
}

actor ReturningShieldBoomerangMasterFunc : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_GiveInventory("CountChildrenFunc",1)
TNT1 A 0 A_JumpIfInventory("ChildCount",1,"Pickup2")
TNT1 A 0 Thing_Remove(0)
stop
Pickup2:
TNT1 A 0 A_CountDown
stop
}
}

actor ReturningShieldBoomerang : ShieldBoomerangBase
{
+INVISIBLE
var int user_NoTranslation;
states
{
Spawn:
TNT1 A 0
Spawn2:
BUST A 1 A_GiveInventory("ReturningShieldBoomerangFunc",1)
loop
Death:
TNT1 A 0 A_SpawnItemEx("ReturningShieldBoomerang2",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
stop
}
}

actor ReturningShieldBoomerang2 : ReturningShieldBoomerang
{
+NOCLIP
+NOINTERACTION
states
{
Spawn:
TNT1 A 0
Spawn2:
BUST A 1 A_GiveInventory("ReturningShieldBoomerangFunc",1)
TNT1 A 0 A_SpawnItemEx("ReturningShieldBoomerang",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
stop
Death:
TNT1 A 0
stop
}
}

actor ReturningShieldBoomerangFunc : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_GiveToTarget("AliveCheck",1)
TNT1 A 0 A_JumpIfInTargetInventory("AliveCheckFlag",1,"Pickup2")
RemoveThis:
TNT1 A 0 A_RemoveMaster
TNT1 A 0 Thing_Remove(0)
stop
Pickup2:
TNT1 A 0 A_JumpIfInTargetInventory("DespawnOldCutter",1,"RemoveThis")
TNT1 A 0 A_JumpIfCloser(60,"Returned")
TNT1 A 0 A_GiveInventory("HeadTowardsTarget_Arg0",1)
TNT1 A 0 A_GiveInventory("WarpMasterHere",1)
stop
Returned:
TNT1 A 0 A_GiveToTarget("ShieldBoomerangReturn",1)
goto RemoveThis
}
}
